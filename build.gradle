apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.stefanbirkner'
version = '2.0.1-SNAPSHOT'

sourceCompatibility = 1.5
targetCompatibility = 1.5

tasks.withType(Compile) {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

dependencies {
    testCompile group: 'junit', name: 'junit', version:'[4.11,)'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version:'[1.3,)'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version:'[1.3,)'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'[1.9.5,)'
}

repositories {
    mavenCentral()
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'sfb-propertyeditors'
               packaging 'jar'
               description 'A few classes for everybody who is working with property editors.'
               url 'http://stefanbirkner.github.io/sfb-propertyeditors/'
               inceptionYear '2011'

               scm {
                   url 'https://github.com/stefanbirkner/sfb-propertyeditors/'
                   connection 'scm:git:git://github.com/stefanbirkner/sfb-propertyeditors.git'
                   developerConnection 'scm:git:git@github.com:stefanbirkner/sfb-propertyeditors.git'
               }

               licenses {
                   license {
                       name 'Apache License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'stefanbirkner'
                       name 'Stefan Birkner'
                       email 'mail@stefan-birkner.de'
                       url 'http://www.stefan-birkner.de'
                       timezone '+1'
                   }
               }
           }
        }
    }
}
